plugins {
    id "java"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.palantir.git-version" version "3.1.0"
    id "com.diffplug.spotless" version "6.13.0"
}

repositories {
    mavenCentral()
}

group = "trocco-io"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Load records from Google Spreadsheets."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "org.embulk:embulk-util-json:0.3.0"
    implementation "org.embulk:embulk-util-timestamp:0.3.0"
//  implementation "com.google.apis:google-api-services-sheets:v4-rev20241008-2.0.0"
    implementation project(path: ":shadow", configuration: "shadow")

    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-junit4:0.11.5"
    testImplementation "ch.qos.logback:logback-classic:1.3.15"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial"
}

embulkPlugin {
    mainClass = "org.embulk.input.google_spreadsheets_java.GoogleSpreadsheetsJavaInputPlugin"
    category = "input"
    type = "google_spreadsheets_java"
}

gem {
    authors = [ "Tetsuro Sano" ]
    email = [ "t3t5u.5an0@gmail.com" ]
    summary = "Google Spreadsheets input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-input-google_spreadsheets_java"
    licenses = [ "MIT" ]
    from("LICENSE.txt")
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()
        toggleOffOn()
    }
}
